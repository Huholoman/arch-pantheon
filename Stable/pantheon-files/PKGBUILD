# Maintainer: Ner0

pkgname=pantheon-files
pkgver=0.1_beta1
pkgrel=1
pkgdesc="A simple, powerful and sexy file manager from elementary (Marlin fork)"
arch=('i686' 'x86_64')
url="https://launchpad.net/pantheon-files"
license=('GPL3')
groups=('pantheon')
depends=('granite>=0.1.1' 'glib2' 'pango' 'gtk3' 'libgee' 'sqlite3' 'dbus-glib' 'gtk-update-icon-cache'
         'libnotify' 'gnome-icon-theme' 'dconf' 'cairo' 'atk' 'desktop-file-utils' 'gconf')
makedepends=('vala' 'cmake' 'pkg-config')
optdepends=('tumbler: for generating thumbnails'
            'contractor-bzr: plugins integration'
            'pantheon-files-plugin-dropbox-bzr: Dropbox integration')
conflicts=('marlin-bin' 'marlin-bzr' 'files-bzr' 'pantheon-files-bzr')
install=pantheon-files.install
source=("https://launchpad.net/$pkgname/0.x/luna-beta1/+download/$pkgname-0.1-luna-beta1.tar.gz")
md5sums=('055b42c7f2f4434c676cb44439d2fbc4')

build() {
  # Fix vala-018 build
  sed -i 's/\(#define FM_TYPE_DIRECTORY_VIEW fm_directory_view_get_type()\)/\#define FM_DIRECTORY_TYPE_VIEW fm_directory_view_get_type()\n\1/' src/fm-directory-view.h

  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DGSETTINGS_COMPILE=OFF
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
