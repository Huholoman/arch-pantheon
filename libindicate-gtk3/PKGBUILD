# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgbase=libindicate
pkgname=libindicate-gtk3
pkgver=0.6.92
pkgrel=1
pkgdesc="Small library for applications to raise flags on DBus for other components of the desktop to pick up and visualize (GTK+ 3 library)"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicate"
license=('LGPL')
depends=('libindicate' 'gtk3')
makedepends=('libdbusmenu' 'dbus-glib' 'gtk2' 'gtk3' 'gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'gnome-doc-utils')
options=('!libtool' '!emptydirs')
source=(http://launchpad.net/libindicate/0.7/$pkgver/+download/$_pkgbase-$pkgver.tar.gz)
md5sums=('f5e0e2c6e4fc9ea1cfb4e9dc52b1e54b')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' examples/{im-client,listen-and-print}.py

  # Fix build
  sed -i '/gmessages\.h/ d' libindicate/indicator.c

  # Fix pyglib name
  sed -i 's/pyglib-2.0-python$PYTHON_VERSION/pyglib-2.0-python2/' configure

  # Fix mono location
  sed -i 's|indicate-sharp-0.1|mono/indicate-sharp|' bindings/mono/indicate/indicate-sharp-0.1.pc.in
  sed -i 's|indicate-gtk-sharp-0.1|mono/indicate-gtk-sharp|' bindings/mono/indicate-gtk/indicate-gtk-sharp-0.1.pc.in
  sed -i 's|$(libdir)/$(ASSEMBLY_NAME)-sharp-0.1|$(libdir)/mono/$(ASSEMBLY_NAME)-sharp|
          s|-package $(ASSEMBLY_NAME) |-package $(ASSEMBLY_NAME)-sharp |' bindings/mono/indicate{,-gtk}/Makefile.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-tests \
              PYTHON=python2
  make
}

package(){
  cd "$srcdir/$_pkgbase-$pkgver"

  make -C libindicate DESTDIR="$pkgdir/" install
  make -j1 -C libindicate-gtk DESTDIR="$pkgdir/" install
  make -C libindicate DESTDIR="$pkgdir/" uninstall
}
