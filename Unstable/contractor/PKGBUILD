# Maintainer: Ner0

pkgname=contractor-bzr
pkgver=12
pkgrel=1
pkgdesc="A sharing service that allows source apps to send their data to registered destination apps"
arch=('i686' 'x86_64')
url="https://launchpad.net/contractor"
license=('GPL3')
depends=('glib2' 'libgee')
makedepends=('bzr' 'gcc' 'vala' 'cmake')
source=('contracts.tar.xz')
md5sums=('1584004699988522035795c2e59215c0')

_bzrtrunk=lp:contractor
_bzrmod=contractor

build() {
  msg "Connecting to Bazaar server...."

  if [ -d "$_bzrmod" ]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver" && cd ..
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  [ -d $_bzrmod-build ] && rm -rf $_bzrmod-build
  cp -r $_bzrmod $_bzrmod-build
  cd $_bzrmod-build
  [ -d build ] && rm -rf build
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
  for i in "$srcdir"/*.contract; do install -m644 "$i" "$pkgdir/usr/share/contractor"; done
}
