# Maintainer: Ner0

pkgname=scratch-bzr
pkgver=946
pkgrel=1
pkgdesc="A text editor written in Vala by elementary"
arch=('i686' 'x86_64')
url="https://launchpad.net/scratch"
license=('GPL3')
depends=('gtksourceview3' 'libgee' 'gobject-introspection' 'glib2' 'desktop-file-utils' 'devhelp'
         'gconf' 'gtk3' 'granite-bzr>=460' 'libpeas' 'pango' 'dconf' 'hicolor-icon-theme' 'vte3' 'libzeitgeist' 'gtkspell3')
makedepends=('bzr' 'cmake' 'vala-016' 'pkg-config')
optdepends=('contractor-bzr: Contractor integration [AUR]'
            'libsoup: Pastebin plugin'
            'pantheon-files-bzr: File Manager plugin [AUR]')
conflicts=('scratch-text-editor')
provides=('scratch-text-editor')
options=('!makeflags')
install=$pkgname.install

_bzrtrunk=lp:scratch
_bzrmod=scratch

build() {
  msg "Connecting to Bazaar server...."

  if [ -d "$_bzrmod" ]; then
    cd "$_bzrmod" && bzr pull "$_bzrtrunk" -r "$pkgver" && cd ..
    msg "The local files are updated."
  else
    bzr branch "$_bzrtrunk" "$_bzrmod" -r "$pkgver"
  fi

  msg "BZR checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$_bzrmod-build"
  cp -rf "$_bzrmod" "$_bzrmod-build"
  cd "$_bzrmod-build"

  sed -i '/--warn-all/d' cmake/GObjectIntrospectionMacros.cmake
  sed -i '/core-tests/d' scratchcore/CMakeLists.txt

  rm -rf build/
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DVALA_EXECUTABLE="$(type -p valac-0.16)" -DGSETTINGS_COMPILE=OFF -DGSETTINGS_LOCALINSTALL=OFF
  make
}

package() {
  cd "$srcdir/$_bzrmod-build/build"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir/" install
}
